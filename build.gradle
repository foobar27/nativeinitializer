/**
 * Copyright (c) 2017 Sebastien Wagener.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *    Sebastien Wagener - initial release
 */
group 'com.github.foobar27'
version "0.1.2"

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
    }
}

apply plugin: 'io.codearte.nexus-staging'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

repositories {
    jcenter()
}

dependencies {
    // Use JUnit test framework
    testCompile 'junit:junit:4.12'
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'native-initializer'
                url 'https://github.com/foobar27/nativeinitializer'
                description 'Specify how native libraries should be loaded'
                packaging 'jar'
                url 'http://www.example.com/example-application'

                scm {
                    connection 'scm:git:git://github.com/foobar27/nativeinitializer.git'
                    developerConnection 'scm:git:ssh://github.com:foobar27/nativeinitializer.git'
                    url 'http://github.com/foobar27/nativeinitializer/tree/master'
                }

                licenses {
                    license {
                        name 'Eclipse Public License (EPL), Version 1.0'
                        url 'http://www.eclipse.org/legal/epl-v10.html'
                    }
                }

                developers {
                    developer {
                        id 'foobar27'
                        name 'Sebastien Wagener'
                        email 'sebastien.wagener@gmail.com'
                    }
                }
                issueManagement {
                    system 'github'
                    url 'https://github.com/foobar27/nativeinitializer/issues'
                }
            }
        }
    }
}
