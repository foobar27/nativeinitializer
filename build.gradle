/**
 * Copyright (c) 2017 Sebastien Wagener.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *    Sebastien Wagener - initial release
 */
group 'com.github.foobar27'
version "0.1.0"

apply plugin: 'java'
apply plugin: 'maven-publish'

repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

dependencies {
    // Use JUnit test framework
    testCompile 'junit:junit:4.12'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
	    from components.java
            pom.withXml {
                asNode().with {
                    appendNode('name', 'nativeinitalizer')
                    appendNode('url',
                               'https://github.com/foobar27/nativeinitializer')
                    appendNode('description',
                               'Specify how native libraries should be loaded')
                    appendNode('licenses').with {
                        appendNode('license').with {
                            appendNode('name', 'Eclipse Public License (EPL), Version 1.0')
                            appendNode('url', ' http://www.eclipse.org/legal/epl-v10.html')
                            appendNode('distribution', 'repo')
                        }
                    }
                }
            }
            tasks.withType(Jar) {
                from(project.projectDir) {
                    include 'LICENSE'
                    into 'META-INF'
                }
            }

            artifact sourceJar
            artifact javadocJar
	}
    }
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}
 
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}